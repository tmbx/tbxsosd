#
# Copyright (C) 2006-2012 Opersys inc.
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import os
Import('env conf_options prefix bindir confdir pydir')

def template_build(target, source, env):
    for a_target, a_source in zip(target, source):
        targetFile = file(str(a_target), "w")
        sourceFile = file(str(a_source), "r")
        targetFile.write(sourceFile.read() % conf_options)
        sourceFile.close()
        targetFile.close()

# Passed to the kctl.sh template.
conf_options['pydir'] = pydir

library_files = [
    'kctllib/__init__.py',
    'kctllib/kbin.py',
    'kctllib/kdatabase.py',
    'kctllib/kexcept.py',
    'kctllib/kiniconfig.py',
    'kctllib/kkeys.py',
    'kctllib/kparams.py',
    'kctllib/ktbxsosdconfigdaemon.py',
    'kctllib/ktbxsosdconfig.py']

command_files = [
    'kctlcmd/__init__.py',
    'kctlcmd/key_cmd.py',
    'kctlcmd/login_cmd.py',
    'kctlcmd/misc_cmd.py',
    'kctlcmd/misc.py',
    'kctlcmd/org_cmd.py',
    'kctlcmd/prof_cmd.py']
    
env.AlwaysBuild(env.Command('kctllib/kconfig.py', 
                            'kctllib/kconfig.py.in', 
                            template_build))
env.AlwaysBuild(env.Command('kctl', 'kctl.sh.in', 
                            [template_build, Chmod("$TARGET", 0755)]))

kctl_sh = File("kctl")
kctl_prog = File("kctl.py")
kctlcmd_lib = env.Dir(os.path.join(pydir, "kctlcmd"))
kctllib_lib = env.Dir(os.path.join(pydir, "kctllib"))

if 'install' in COMMAND_LINE_TARGETS and not env.GetOption('clean'):
    env.Install(pydir, kctl_prog)
    env.Install(bindir, kctl_sh)

    for s in command_files:
        env.Install(kctllib_lib, s)
        
    for s in library_files:
        env.Install(kctlcmd_lib, s)

env.Alias('install', pydir)
